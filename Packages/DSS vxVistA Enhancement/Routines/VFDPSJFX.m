VFDPSJFX ;DSS/RJS - Cleanup bad entries in File #55 ; 10/06/2015
 ;;2015.0;VXVISTA DSS,INC;;;Build 1
 ;Copyright 1995-2015, Document Storage Systems, Inc., All Rights Reserved
 ;
 Q
GET ; FIND BAD RECORDS
 N VFDIEN,VFDCNT
 S (VFDCNT,VFDIEN)=0 F  S VFDIEN=$O(^PS(55,VFDIEN)) Q:'VFDIEN  D
 .I $D(^PS(55,VFDIEN,5.1)),'$D(^PS(55,VFDIEN,0)) D
 ..S ^TMP("VFDPSFX",$J,VFDIEN)="",VFDCNT=VFDCNT+1
 ;
FIX ;
 N VFDDT,VFDPS550
 S VFDDT=$$FMADD^XLFDT(DT,30),VFDPS550=^PS(55,0)
 S ^XTMP("VFDPSJFX",0)=VFDDT_"^"_DT_"^"_VFDPS550
 M ^XTMP("VFDPSJFX")=^TMP("VFDPSFX",$J)
 S VFDIEN=0 F  S VFDIEN=$O(^TMP("VFDPSFX",$J,VFDIEN)) Q:'VFDIEN  D
 .N DO,DA,DD,DIC,PSIVFN,DIK
 .S ^PS(55,VFDIEN,0)=VFDIEN,DIK="^PS(55,",DIK(1)=.01,DA=VFDIEN D EN^DIK
 ;
RESET0 ;
 N VFDCNTR
 S (VFDCNTR,VFDIEN)=0 F  S VFDIEN=$O(^PS(55,"B",VFDIEN)) Q:'VFDIEN  S VFDCNTR=VFDCNTR+1
 S $P(^PS(55,0),"^",4)=VFDCNTR
 K ^TMP("VFDPSFX",$J)
 Q
 ;
UNDO ;
 N VFDIEN
 S VFDIEN=0 F  S VFDIEN=$O(^XTMP("VFDPSJFX",VFDIEN)) Q:'VFDIEN  D
 .M ^PS(55,VFDIEN)=^XTMP("VFDPSJFX",VFDIEN)
 S $P(^PS(55,0),"^",4)=$P(^XTMP("VFDPSJFX",0),"^",6)
 Q
