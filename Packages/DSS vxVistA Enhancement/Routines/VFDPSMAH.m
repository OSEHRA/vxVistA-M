VFDPSMAH ;DSS/RJS - VFD PSO MED ADMIN HISTORY RPC; 05/26/2015 16:10
 ;;2013.1;DSS,INC VXVISTA OPEN SOURCE;**54**;28 Jan 2013;Build 2
 ;Copyright 1995-2015,Document Storage Systems Inc. All Rights Reserved
 ;
 Q
 ;
HSQUERY(ROOT,ORDFN,ID,ORALPHA,OROMEGA,ORDTRNG,REMOTE,ORMAX,ORFHIE) ;
 N ORATTEND,ORDG,ORDTIM,OREND,ORGRP,ORLIST,ORPD,ORPRES,ORSHORT,ORTIT,POP,ZTSTOP,ORNO,GMRVSTR,RPT
 Q:'$D(^DPT(ORDFN))
 D START^ORWRP(80,"ORDB^VFDPSMAH(.ROOT,.ORDFN,.ID,.ORALPHA,.OROMEGA,.ORDTRNG,.REMOTE,.ORMAX,.ORFHIE)")
 K ^TMP("VFDMED",$J),^TMP("VFDADMIN",$J),^TMP("ORR",$J)
 Q
PRTOUT(ROOT,ORDFN,OREXAMID,ORALPHA,OROMEGA,ORDTRNG,REMOTE,ORMAX,ORFHIE)        ;Print OPT Med Hist
 N ORYM,TEXT,RESULTS,IOT
 D
 .N VFDTMP M VFDTMP(1)=IO(1)
 .N IO
 .N VFD2 S VFD2="" F  S VFD2=$O(VFDTMP(1,VFD2)) Q:VFD2=""  S:VFD2[":" IO(0)=VFD2
 .S:'$G(ORMAX) ORMAX=0
 .D HFSOPEN^VFDPSMAH1("RPC") I POP S ^TMP("VFDPSMAH",$J,1)="ERROR: UNABLE TO ACCESS HFS DIRECTORY "_$$GET^XPAR("DIV","PSB HFS SCRATCH"),^TMP("PSBO",$J,2)="PLEASE CHECK DIRECTORY WRITE PRIVILEDGES." Q
 .U IO D ORDB^VFDPSMAH(.ROOT,.ORDFN,.ID,.ORALPHA,.OROMEGA,.ORDTRNG,.REMOTE,.ORMAX,.ORFHIE)
 .D HFSCLOSE^VFDPSMAH1("RPC")
 .S RESULTS=$NAME(^TMP("VFDPSMAH",$J))
 U IO
 Q:'$L(RESULTS)
 S PAGE=1,TEXT="PATIENT OUTPAITEN MEDICATION HISTORY ("_$$FMTE^XLFDT(ORALPHA)_" - "_$$FMTE^XLFDT(OROMEGA)_")"
 D HEAD^VFDPSMAH1(ORDFN,PAGE,TEXT,$G(STATION))
 D HURL^VFDPSMAH1(.RESULTS,ORDFN,TEXT,1)
 I $L($G(RESULTS)) K @RESULTS
 K ^TMP("VFDMED",$J),^TMP("VFDADMIN",$J),^TMP("ORR",$J)
 Q
 ;
ORDB(ROOT,DFN,ID,ORDBEG,ORDEND,DTRANGE,REMOTE,ORMAX,ORFHIE) ;Order Summary for Date Range
 Q:'$G(DFN)
 I $L($G(DTRANGE)),'$G(ORDBEG) S ORDBEG=$$FMADD^XLFDT(DT,-DTRANGE),ORDEND=$$NOW^XLFDT
 Q:'$G(ORDBEG)  Q:'$G(ORDEND)
 N ORVP,XQORNOD,ORSSTRT,ORSSTOP
 S ORVP=DFN_";DPT(",XQORNOD=1,ORSSTRT=ORDBEG,ORSSTOP=ORDEND,ORTIT="OUTPATIENT MED ADMIN HISTORY"
 D ORCVA,ORC(ROOT,ORALPHA,OROMEGA,ORMAX,ORDBEG,ORDEND,ORFHIE)
 Q
ORC(ROOT,ORALPHA,OROMEGA,ORMAX,ORDBEG,ORDEND,OREXT) ; Gather data
 N CNTR,EXAMID,IEN,PSOHDR,PSODFN,PTR,VA,VAERR,VAIP,VAPD,VFD,VFDDT,VFDLN,VFDMDT,VFDMED,VFDPTR
 S RPT="ORC^ORDV04(.ROOT,ORALPHA,OROMEGA,ORMAX,ORDBEG,ORDEND,OREXT)",PSODFN=DFN,IEN=0
 D @RPT
 K ^TMP("ORDATA",$J)
 S GMRA="0^0^111" D ^GMRADPT,AL^VFDPSMAH1,PT(DFN,.PSOHDR),MEDS,RPT
 Q
 ;
ORCVA ;Current Orders
 N %,%H,%I,DX,DY,I,II,J,K,ODATE,ORAGE,ORDAD,ORDOB,ORFLAG,ORI,ORIFN
 N ORL,ORLIST,ORLST,ORMD,ORNP,ORODT,ORPD,ORPNM,ORPV,ORREQ,ORSEX
 N ORSSN,ORSTOP,ORSTRT,ORSTS,ORASTS,ORTERM,ORTM,ORTS,ORTX,ORUSER,ORWARD
 N ORSLTR,ORAW,VA,VAROOT,X,X0,X1,X3,Y,Z,VFDBGN,LAPSE,VFDIEN,VFDORD
 S ORAW=$S(+$$GET^XPAR("SYS","OR ORDER SUMMARY CONTEXT",1,"I"):"AW",1:"")
 U IO
 S VFDBGN=ORDBEG F  S VFDBGN=$O(^VFD(21653.79,"PVD",DFN,VFDBGN)) Q:'VFDBGN  D
 .S VFDIEN=0 F  S VFDIEN=$O(^VFD(21653.79,"PVD",DFN,VFDBGN,VFDIEN)) Q:'VFDIEN  D
 ..S VFDORD=+$G(^VFD(21653.79,VFDIEN,0)),VFDORR(VFDORD)=""
 ..I $D(^OR(100,VFDORD)),+$P(^OR(100,VFDORD,0),U,2)=DFN D
 ...N SDATE S SDATE=$P(^OR(100,VFDORD,0),U,8)
 ...S ORVP=DFN_";DPT("
 ...D EN^VFDPSMAH1(ORVP,,2,,ORDBEG,ORDEND,1) ;get current orders. ORLIST is set in ORQ1
 ...M ^TMP("MEDS",$J)=^TMP("ORR",$J)
 ...D GETS^DIQ(21653.79,VFDIEN_",","**","IE","VFDATA","VFDERR")
 ...D PARSE^VFDPSOMA(.VFDATA,VFDIEN)
 ...D ADMIN  K VFDATA
 Q
 ;
PT(DFN,PSOHDR,PSOCONT,PSODT) ; Gather Patient data
 ; DFN:     Patient File IEN
 ; PSOCONT: True if this is a continuation page
 ; PSODT:   Date of Pt Information (Default to DT)
 W:$Y>1 @IOF
 W:$X>1 !
 S:'$G(PSODT) PSODT=DT
 ; BUILD PSOHDR WITH ALL HEADER STUFF
 D:'$D(PSOHDR("NAME"))
 .S VAIP("D")="LAST"
 .D DEM^VADPT
 .S PSOHDR("NAME")=VADM(1)
 .S PSOHDR("SSN")=$P(VADM(2),U,2)
 .S PSOHDR("DOB")=$P(VADM(3),U,2)
 .S PSOHDR("AGE")=VADM(4)
 .S PSOHDR("SEX")=$P(VADM(5),U,2)
 .K VADM
 .S GMRVSTR="HT" D EN6^GMRVUTL
 .S X=+$P(X,U,8) S:X X=X*2.54\1 S PSOHDR("HEIGHT")=$S(X:X_"cm",1:"*")
 .S GMRVSTR="WT" D EN6^GMRVUTL
 .S X=+$P(X,U,8) S:X X=X*.45\1 S PSOHDR("WEIGHT")=$S(X:X_"kg",1:"*")
 Q
 ;
MEDS ; Gather Med data
 N X,Y,Z,VFD,VFDATA,VFDERR,PTR,OACTION,VFDCNTR,VFDIEN,VFDLOC,VFDRX,VFDRXNUM,VFDVST,VFDDRG,VFDEPT,VFDADMLN
 Q:'$D(^TMP("MEDS",$J))
 S VFDDT=0 F  S VFDDT=$O(^TMP("MEDS",$J,VFDDT)) Q:'VFDDT  D
 .S ORLIST=VFDDT
 .S PTR=0 F  S PTR=$O(^TMP("MEDS",$J,VFDDT,PTR)) Q:'PTR  D
 ..I $P(^TMP("MEDS",$J,VFDDT,PTR),U,2)'="O RX"!($P(^TMP("MEDS",$J,VFDDT,PTR),U,7)="pend") K ^TMP("ORR",$J,VFDDT,PTR),^TMP("MEDS",$J,VFDDT,PTR) Q
 ..S VFDVST=$P($G(^TMP("MEDS",$J,VFDDT,PTR)),U,1),VFDRX=$O(^PSRX("APL",+$G(VFDVST),0)),OACTION=$P((VFDVST),";",2)
 ..I '$D(VFDORR(+VFDVST)) K ^TMP("MEDS",$J,VFDDT,PTR),^TMP("ORR",$J,VFDDT,PTR) Q
 ..S VFDDRG=$$GET1^DIQ(52,VFDRX,6,"I"),VFDEPT=$$GET1^DIQ(50,VFDDRG,21600.01,"I")
 ..I $G(VFDEPT) K ^TMP("MEDS",$J,VFDDT,PTR),^TMP("ORR",$J,VFDDT,PTR) Q
 ..S VFDRXNUM=$$GET1^DIQ(52,VFDRX,.01,"E"),VFDLOC=$$GET1^DIQ(52,VFDRX,5,"E")
 ..S ^TMP("VFDMED",$J,+VFDDT,PTR,+$G(VFDVST),0)=$P($G(^TMP("MEDS",$J,VFDDT,PTR)),U,4)_U_$P($G(^TMP("MEDS",$J,VFDDT,PTR)),U,5)_U_$P($G(^TMP("MEDS",$J,VFDDT,PTR)),U,7)
 ..S ^TMP("VFDMED",$J,+VFDDT,PTR,+$G(VFDVST),0)=^TMP("VFDMED",$J,+VFDDT,PTR,+$G(VFDVST),0)_U_VFDRXNUM_U_VFDLOC_U_$P($G(^TMP("MEDS",$J,VFDDT,PTR)),U,3)
 ..S VFDCNTR=1,VFDLN=0 F  S VFDLN=$O(^TMP("MEDS",$J,VFDDT,PTR,"TX",VFDLN)) Q:'VFDLN  D
 ...S ^TMP("VFDMED",$J,+VFDDT,PTR,+$G(VFDVST),VFDLN)=$G(^TMP("MEDS",$J,VFDDT,PTR,"TX",VFDLN)),VFDCNTR=VFDCNTR+1
 ..D ORD
 ..S VFDIEN=0 F  S VFDIEN=$O(^TMP("VFDADMIN",$J,+VFDDT,+$G(VFDVST),VFDIEN)) Q:'VFDIEN  D
 ...S VFDADMLN=0 F  S VFDADMLN=$O(^TMP("VFDADMIN",$J,+VFDDT,+$G(VFDVST),VFDIEN,VFDADMLN)) Q:'VFDADMLN  D
 ....S ^TMP("VFDMED",$J,+VFDDT,PTR,+$G(VFDVST),VFDCNTR,VFDIEN,VFDADMLN)=$G(^TMP("VFDADMIN",$J,+VFDDT,+$G(VFDVST),VFDIEN,VFDADMLN))
 K ^TMP("MEDS",$J),^TMP("VFDADMIN",$J),VFDORR
 Q
ORD ; Gather Order data
 I $D(^OR(100,+$G(VFDVST),8,OACTION,0)) S X=^(0) D
 .N NATURE,SIGSTS,NURSE,REVIEW,ORREQ
 .S ORREQ=$P(X,"^",3)
 .S SIGSTS=$P(X,"^",4),NATURE=$P(X,"^",12),NURSE=$P(X,"^",8),REVIEW=$P(X,"^",18)
 .S ORREQ=$S(ORREQ:$S($D(^VA(200,ORREQ,0)):$P(^(0),"^"),1:"UNKNOWN"),1:"UNKNOWN"),X=$P(ORREQ,",",2),ORREQ=$E($P(ORREQ,","),1,8)_","_$S($E(X)=" ":$E(X,$F(X,"")),1:$E(X))
 .I $L(SIGSTS) S SIGSTS=$$SET^ORPRS04(100.008,4,SIGSTS)
 .I NATURE S NATURE=$P(^ORD(100.02,NATURE,0),"^")
 .I NURSE S NURSE=$P($G(^VA(200,NURSE,0)),"^",2)
 .I REVIEW S REVIEW=$P($G(^VA(200,REVIEW,0)),"^",2)
 .S ^TMP("VFDMED",$J,+VFDDT,PTR,+$G(VFDVST),0)=^TMP("VFDMED",$J,+VFDDT,PTR,+$G(VFDVST),0)_U_$G(ORREQ)
 .S ^TMP("VFDMED",$J,+VFDDT,PTR,+$G(VFDVST),VFDCNTR,0)=$G(NURSE)_U_$G(REVIEW)_U_$E($G(NATURE),1,18)_U_$E($G(SIGSTS),1,25)
 Q
RPT ; Print Med Admin History
 I $$S^%ZTLOAD S (ZTSTOP,OREND,DIROUT)=1 W !!,"TASKED Report stopped by "_$P(^VA(200,DUZ,0),U) Q
 N DIRUT,DUOUT,ORBOT,ORSPG,ORLINE,YENKO,SHELBY,ORLST,ORREQ,VFDTODAY
 N ORAGE,ORDOB,ORL,ORNP,ORPNM,ORPV,ORSEX,ORSSN,ORTS,ORWARD
 I $G(ORNO),'$O(^TMP("ORR",$J,ORLIST,0)) Q
 S YENKO=$$GET^XPAR("ALL","OR PRINT NO ORDERS ON SUM",1,"I"),SHELBY=""
 S:$D(^TMP("ORR",$J)) SHELBY=$O(^TMP("ORR",$J,ORLIST,0))
 ;E  S SHELBY=""
 ;YENKO=0 or "", if you don't want to print a page when no orders are present
 ;      1 to print the page with "NO ORDERS" on it.
 I 'YENKO,'SHELBY D  Q
 .S $P(ORLINE,"=",IOM+1)="",ORBOT=$S(IOSL<254:IOSL,1:254),Y=+ORVP D
 ..D END^ORUDPA I '$G(ORSPG) S ORSPG=1,VFDTODAY=0
 ..D HDRDT,PTOP^VFDPSMAH1(ORSPG,ORTIT,ORSSTRT,ORSSTOP),PRT K:$D(^TMP("ORR",$J)) ^TMP("ORR",$J,ORLIST)
 .W !!?(IOM-44)\2,"*****    E N D    O F    R E P O R T    ****"
 S $P(ORLINE,"=",IOM+1)="",ORBOT=$S(IOSL<254:IOSL,1:254),Y=+ORVP
 D END^ORUDPA
 S ORREQ("O")=""
 I '$G(ORSPG) S ORSPG=1,VFDTODAY=0 D
 .D HDRDT,PTOP^VFDPSMAH1(ORSPG,ORTIT,ORSSTRT,ORSSTOP)
 I '$O(^TMP("ORR",$J,ORLIST,0)) W !!?3,"No orders.",!!! D  Q
 .F I=$Y:1:ORBOT-5 W !
 .W @IOF
 .I '$G(ORSEND) Q
 S (ORLST,OREND)=0
 F  S ORLST=$O(^TMP("ORR",$J,ORLIST,ORLST)) Q:'ORLST!$D(DUOUT)  D PRT
END W @IOF
 W !!!?(IOM-44)\2,"*****    E N D    O F    R E P O R T    ****"
 W @IOF
 K ORSEND,ORSPG,ORCONT
 Q
 ;
PRT ; Print patient information
 W !,"Patient: "_PSOHDR("NAME"),?40,"MRN: ",$G(VA("MRN")),?59,"DOB:",PSOHDR("DOB"),"(",PSOHDR("AGE"),")"
 W !,"Sex:     ",PSOHDR("SEX"),?38,"Ht/Wt: ",PSOHDR("HEIGHT"),"/",PSOHDR("WEIGHT")
 W !,PSOHDR("AL",0) S PTR=0
 F  S PTR=$O(PSOHDR("AL",PTR)) Q:'PTR  W !,?15,PSOHDR("AL",PTR,0)
 W:'$O(PSOHDR("AL",0)) !,?15,"No Known Allergies"
 W !,PSOHDR("AD",0) S PTR=0
 F  S PTR=$O(PSOHDR("AD",PTR)) Q:'PTR  W !,?15,PSOHDR("AD",PTR,0)
 W:'$O(PSOHDR("AD",0)) !,?15,"None on file."
 W !,$TR($J("",IOM)," ","=")
 I '$D(^TMP("VFDMED",$J)) D  Q
 .W !!,?IOM-$L("*** No Outpatient Meds Administration History ***")/2,"*** No Outpatient Meds Administration History ***",!
 W !,"Ord'd",?9,"ST  ","Item Ordered",?54,"Start",?66,"Stop"
 W !,?58,"Requestor"
 D MEDRPT
 ;
 Q
MEDRPT ; PRINT MEDS Information
 N STPDT,STRTDT,STTDT
 S VFDDT=0 F  S VFDDT=$O(^TMP("VFDMED",$J,VFDDT)) Q:'VFDDT  D
 .S VFDPTR=0 F  S VFDPTR=$O(^TMP("VFDMED",$J,VFDDT,VFDPTR)) Q:'VFDPTR  D
 ..S VFD=0 F  S VFD=$O(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD)) Q:'VFD  D
 ...D:(($Y+15)>IOSL) CONT(ORSPG,ORTIT,ORSSTRT,ORSSTOP)
 ...W !,$TR($J("",IOM)," ","=")
 ...W !,?IOM-$L("*** Outpatient Meds Administration History ***")/2,"*** Outpatient Meds Administration History ***",!
 ...W !,"Order: ",VFD,?20,"Rx# ",$P(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,0),U,4),?40,"Clinic: ",$P(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,0),U,5)
 ...S STTDT=$P(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,0),U,6),STTDT=STTDT_0000,STRTDT=$E(STTDT,4,5)_"/"_$E(STTDT,6,7)_"/"_$E(STTDT,2,3),ORDTIM=$E(STTDT,9,10)_":"_$E(STTDT,11,12)
 ...W !!,STRTDT,?9,$E($P(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,0),U,3),1),?12,$G(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,1))
 ...S STTDT=$P(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,0),U),STRTDT=$E(STTDT,4,5)_"/"_$E(STTDT,6,7)_"/"_$E(STTDT,2,3)
 ...S STPDT=$P(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,0),U,2),STTDT=$E(STPDT,4,5)_"/"_$E(STPDT,6,7)_"/"_$E(STPDT,2,3)
 ...W ?54,STTDT,?66,STPDT
 ...W !,?3,ORDTIM,?12,$P($G(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,2)),U),?57,$P($G(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,0)),U,7)
 ...S PTR=2 F  S PTR=$O(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR)) Q:'PTR  D
 ....D:$D(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,0)) SIGRPT
 ....W !,?12,$P($G(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR)),U)
 ...I $G(^TMP("ORR",$J,VFDDT,"TOT"))=VFDPTR K ^TMP("ORR",$J,VFDDT)
 ...E  K ^TMP("ORR",$J,VFDDT,VFDPTR) Q
 Q
SIGRPT ; Print Sig
 N SIGDATA
 S SIGDATA=^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,0)
 W ! I $L($P(SIGDATA,U,1)) W ?9,"Nrs: "_$P(SIGDATA,U,1)
 I $L($P(SIGDATA,U,2)) W ?19,"Chrt: "_$P(SIGDATA,U,2)
 I $L($P(SIGDATA,U,3)) W ?30,"Typ: "_$P(SIGDATA,U,3)
 I $L($P(SIGDATA,U,4)) W ?54,"Sgn: "_$P(SIGDATA,U,4)
 D ADMRPT
 Q
SPACE ; BLANK LINE
 S CNTR=CNTR+1,^TMP("ORDATA",$J,CNTR)=""
 Q
ADMRPT ; PRINT VFD PSO MED ADMIN Information
 N ADMPTR,ADMLN1
 D:(($Y+6)>IOSL) CONT(ORSPG,ORTIT,ORSSTRT,ORSSTOP)
 I '$O(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,0)) W !,?IOM-$L("****No OUTPATIENT MEDICATION ADMINISTRATION HISTORY FOUND*****")/2,"****No OUTPATIENT MEDICATION ADMINISTRATION HISTORY FOUND*****",! Q
 I $O(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,0)) D
 .S ADMLN1=0 F  S ADMLN1=$O(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,ADMLN1)) Q:'ADMLN1  D
 ..W ! S ADMPTR=0 F  S ADMPTR=$O(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,ADMLN1,ADMPTR)) Q:'ADMPTR  D
 ...D ADMLN Q:$G(VFDLN)="" 
 ...W !,?3,VFDLN
 W !
 Q
ADMLN ; Organize Med Admin Output
 S VFDLN=""
 I ADMPTR=1 D
 .S VFDLN="Visit D/T:"_$P($G(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,ADMLN1,ADMPTR)),U,1) W !,?3,VFDLN
 .S VFDLN="Admin D/T:"_$P($G(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,ADMLN1,ADMPTR)),U,2) Q
 I ADMPTR=2 D
 .S VFDLN="Administered By: "_$P($G(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,ADMLN1,ADMPTR)),U,1) W !,?3,VFDLN
 .S VFDLN="Quantity Administered: "_$P($G(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,ADMLN1,ADMPTR)),U,2) Q
 I ADMPTR=3 D  Q
 .I $P($G(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,ADMLN1,ADMPTR)),U,1)'="" S VFDLN="Who Administered?: "_$P($G(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,ADMLN1,ADMPTR)),U,1) W !,?3,VFDLN
 .I $P($G(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,ADMLN1,ADMPTR)),U,2)'="" S VFDLN="Monitored Re-Admin Reason: "_$P($G(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,ADMLN1,ADMPTR)),U,2)
 I ADMPTR=4 D  Q
 .I $P($G(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,ADMLN1,ADMPTR)),U,1)'="" S VFDLN="DOSE MISSED REASON: "_$P($G(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,ADMLN1,ADMPTR)),U,1)
 .E  S VFDLN=""
 .I $P($G(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,ADMLN1,ADMPTR)),U,2)'="" S VFDLN=VFDLN_"DOSE HELD REASON: "_$P($G(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,ADMLN1,ADMPTR)),U,2)
 I ADMPTR=5,$G(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,ADMLN1,ADMPTR))'="^" D  Q
 .S VFDLN="USER ENTERING IN ERROR: "_$P($G(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,ADMLN1,ADMPTR)),U,1) W !,?3,VFDLN
 .S VFDLN="ENTERED IN ERROR D/T: "_$P($G(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,ADMLN1,ADMPTR)),U,2)
 I ADMPTR=6,$G(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,ADMLN1,ADMPTR))'="" S VFDLN="COMMENTS: "_$G(^TMP("VFDMED",$J,VFDDT,VFDPTR,VFD,PTR,ADMLN1,ADMPTR)) Q
 Q
 ;
ADMIN ; Store ^TMP VFD PSO Med ADMIN data
 S:$G(VFDATA(.02)) VFDATA(.02)=VFDATA(.02)_0000,VFDST=$E($G(VFDATA(.02)),4,5)_"/"_$E($G(VFDATA(.02)),6,7)_"/"_$E($G(VFDATA(.02)),2,3)
 S:$G(VFDATA(.02)) VFDTIM=$E($G(VFDATA(.02)),9,10)_":"_$E($G(VFDATA(.02)),11,12)
 S:$G(VFDST) VFDATA(.02)=VFDST_"@"_VFDTIM
 K VFDST,VFDTIM
 S ^TMP("VFDADMIN",$J,+ORLIST,VFDORD,VFDIEN,1)=$G(VFDATA(.02))
 S:$G(VFDATA(1.01)) VFDATA(1.01)=VFDATA(1.01)_0000,VFDST=$E($G(VFDATA(1.01)),4,5)_"/"_$E($G(VFDATA(1.01)),6,7)_"/"_$E($G(VFDATA(1.01)),2,3)
 S:$G(VFDATA(1.01)) VFDTIM=$E($G(VFDATA(1.01)),9,10)_":"_$E($G(VFDATA(1.01)),11,12)
 S:$G(VFDST) VFDATA(1.01)=VFDST_"@"_VFDTIM
 K VFDST,VFDTIM
 S ^TMP("VFDADMIN",$J,+ORLIST,VFDORD,VFDIEN,1)=^TMP("VFDADMIN",$J,+ORLIST,VFDORD,VFDIEN,1)_U_$G(VFDATA(1.01))
 S ^TMP("VFDADMIN",$J,+ORLIST,VFDORD,VFDIEN,2)=$G(VFDATA(.03))_U_$G(VFDATA(1.02))
 S ^TMP("VFDADMIN",$J,+ORLIST,VFDORD,VFDIEN,3)=$G(VFDATA(1.03))_U_$G(VFDATA(2))
 S ^TMP("VFDADMIN",$J,+ORLIST,VFDORD,VFDIEN,4)=$G(VFDATA(3.02))_U_$G(VFDATA(4.02))
 S:$G(VFDATA(5.01)) VFDST=$E($G(VFDATA(5.01)),4,5)_"/"_$E($G(VFDATA(5.01)),6,7)_"/"_$E($G(VFDATA(5.01)),2,3)
 S:$G(VFDATA(5.01)) VFDTIM=$E($G(VFDATA(5.01)),9,10)_":"_$E($G(VFDATA(5.01)),11,12)
 S:$G(VFDST) VFDATA(5.01)=VFDST_"@"_VFDTIM
 K VFDST,VFDTIM
 S ^TMP("VFDADMIN",$J,+ORLIST,VFDORD,VFDIEN,5)=$G(VFDATA(5.01))_"^"_$G(VFDATA(5.03))
 S ^TMP("VFDADMIN",$J,+ORLIST,VFDORD,VFDIEN,6)=$G(VFDATA(6))
 Q
CONT(PG,TITLE,START,STOP) ;continuation header for printouts
 Q:'$G(PG)  Q:'$D(TITLE)
 N ORHLINE S $P(ORHLINE,"=",IOM+1)="",X=$P($$HTE^XLFDT($H),":",1,2),PG=PG+1
 W !,#,ORHLINE,!,"Continuing ",ORTIT,?(IOM-($L(PG)+5)),"Page "_PG
 S X=$P($$HTE^XLFDT($H),":",1,2) W !,?IOM-($L(X)+8),"Printed "_X,!
 W ORHLINE,!,"Ord'd",?9,"ST  ","Item Ordered",?54,"Start",?66,"Stop"
 W !,?58,"Requestor"
 Q
HDRDT ; Convert date ranges for header
 N STRTDT
 I '+$G(ORDTRNG),$G(ORDTRNG)'=""  S VFDTODAY=1 Q
 S STRTDT=$E(ORSSTRT,4,5)_"/"_$E(ORSSTRT,6,7)_"/"_$E(ORSSTRT,2,3),$P(ORSSTRT,U,2)=STRTDT
 S STRTDT=$E(ORSSTOP,4,5)_"/"_$E(ORSSTOP,6,7)_"/"_$E(ORSSTOP,2,3),$P(ORSSTOP,U,2)=STRTDT
 I ORDTRNG=50000 S $P(ORSSTRT,U,2)="EARLIEST "
 Q
