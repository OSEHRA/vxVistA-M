VFDPSJCA ;DSS/SMH/SMP - Cassette Labels; 07/17/2015 08:30
 ;;1.0;DSS OPEN SOURCE MODIFICATIONS;;;Build 29
 ; Code copied from PSGL, and trying to clean this up... oh well.
 N PSGDT
 N PSJSYSL,PSJSYSP,PSJSYSP0,PSJSYSU
 D ENCV^PSGSETU
 ;
ASK ;
 N PSGINWD,PSJDCEXP,PSJPAD,PSJPAGE,PSJPBID,PSJPCAF,PSJPDD,PSJPDOB,PSJPDX
 N PSJPHT,PSJPHTD,PSJPPID,PSJPRB,PSJPSEX,PSJPSSN,PSJPTD,PSJPTS,PSJPTSP
 N PSJPWD,PSJPWT,PSJPWTD,PSJSYSW,PSJSYSW0,PSGINCL
 N VAIN,VA,VADM,VAPD,X,Y
 N PSGSS,PSGSSH
 S PSGSSH="LBL"
 F  D ^PSGSEL Q:"^"[PSGSS  D
 . N PSGP
 . N CL,CG
 . N PSGLWD,PSGLWG,PSGLWDN,PSJPWDN
 . D @PSGSS
 . Q:(Y<1)
 . N ZTSAVE N % F %="PSGLWG","PSGLWD","PSGLWDN","PSGP" S ZTSAVE(%)=""
 . N %ZIS S %ZIS="MQ",%ZIS("A")="Label Printer: ",%ZIS("B")=$P(PSJSYSL,"^",2)
 . N ZTSK
 . D EN^XUTMDEVQ("EN"_PSGSS_"^VFDPSJCA","Print Cassette Labels",.ZTSAVE,.%ZIS,1)
 . I $D(ZTSK) W !!,"Queued as task "_ZTSK_".",!
 QUIT
G ;
 ; ZEXCEPT: PSGLWG,PSGLWGN,X,Y
 N DIC S DIC="^PS(57.5,",DIC(0)="QEAMIZ",DIC("A")="Select WARD GROUP: " W ! D ^DIC D
 . I X="^OTHER" S (PSGLWG,PSGLWGN)="^OTHER",Y=1 Q
 . I Y>0 S PSGLWG=+Y,PSGLWGN=Y(0,0)
 QUIT
 ;
W ;
 ; ZEXCEPT: PSGLWD,PSGLWDN,DIC,Y
 N DIC S DIC="^DIC(42,",DIC(0)="QEAMIZ",DIC("A")="Select WARD: " W ! D ^DIC K DIC S:Y>0 PSGLWD=+Y,PSGLWDN=Y(0,0)
 QUIT
 ;
P ;
 ; ZEXCEPT: PSGP
 N PSJPR D ^PSJP S Y=PSGP Q
 ;
C ;
 N DIR S DIR(0)="FAO",DIR("A")="Select CLINIC: "
 S DIR("?")="^D CDIC^PSGVBW" W ! D ^DIR
CDIC ;
 ; ZEXCEPT: X,Y,CL
 N DIC S DIC="^SC(",DIC(0)="QEMIZ" D ^DIC K DIC S:+Y>0 CL=+Y
 W:X["?" !!,"Enter the clinic you want to use to select patients for processing.",!
 Q
L ;
 N DIR S DIR(0)="FAO",DIR("A")="Select CLINIC GROUP: "
 S DIR("?")="^D LDIC^PSGVBW" W ! D ^DIR
LDIC ;
 ; ZEXCEPT: X,Y,CG
 N DIC S DIC="^PS(57.8,",DIC(0)="QEMI" D ^DIC K DIC S:+Y>0 CG=+Y
 W:X["?" !!,"Enter the name of the clinic group you want to use to select patients for processing."
 Q
 ;
ENG ;
 ; ZEXCEPT: PSGLWG
 N PSGLWD,PSGLWDN F PSGLWD=0:0 S PSGLWD=$O(^PS(57.5,"AC",PSGLWG,PSGLWD)) Q:'PSGLWD  S PSGLWDN=$P($G(^DIC(42,PSGLWD,0)),"^") D ENW1
 Q
 ;
ENW ;
 ; ZEXCEPT: PSGLWD
 N PSGLWG,PSGLWGN S PSGLWG=$O(^PS(57.5,"AB",PSGLWD,0)),PSGLWGN="" I PSGLWG,$D(^PS(57.5,PSGLWG,0)),$P(^(0),"^")]"" S PSGLWG=$P(^(0),"^")
 ;
ENW1 ;
 ; ZEXCEPT: PSGLWDN
 N %,PSGDT,PSGOP,DFN,PSGP D NOW^%DTC S PSGDT=% U IO F PSGOP=0:0 S (DFN,PSGOP,PSGP)=$O(^DPT("CN",PSGLWDN,PSGOP)) Q:'PSGOP  D ENP
 Q
ENP ;
 ; ZEXCEPT: PSGP
 N DFN,RET S DFN=$G(PSGP),RET=$$EN^VFDPSPRT("CASSETTE")
 ;D CASSETTE^VFDPSPRT(PSGP)
 Q
 ;
ENL ;
 ; ZEXCEPT: CG
 N CL S CL="" F  S CL=$O(^PS(57.8,"AD",CG,CL)) Q:CL=""  D ENC
 Q
 ;
ENC ;
 K ^TMP("PSJCI",$J)
 N STDTE,CLINIC,JDFN,DFN,PSGP,PSGOP
 S STDTE=0 F  S STDTE=$O(^PS(55,"AUDC",STDTE)) Q:'STDTE  S CLINIC=0 F  S CLINIC=$O(^PS(55,"AUDC",STDTE,CLINIC)) Q:'CLINIC  D
 . S JDFN=0 F  S JDFN=$O(^PS(55,"AUDC",STDTE,CLINIC,JDFN)) Q:'JDFN  S ^TMP("PSJCI",$J,JDFN)=""
 S DFN="" F  S DFN=$O(^TMP("PSJCI",$J,DFN)) Q:'DFN  S (PSGOP,PSGP)=DFN D ENP
 Q
 ;
POST ; [KIDS] Delete Cassette option for OMH. I don't want headaches.
 N STATION S STATION=$P($$SITE^VASITE(),U,3)
 I $E(STATION,1,3)'=111 QUIT
 D DELETE^XPDMENU("PSJU PLMGR","VFD PSJ PRINT CASSETTE LABELS")
 QUIT
