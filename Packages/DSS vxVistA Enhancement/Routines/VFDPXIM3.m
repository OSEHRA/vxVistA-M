VFDPXIM3 ;DSS/PDW/LM - GENERATE IMMUNIZATION MESSAGE ; 15 Mar 2016 0715
 ;;15.0;DSS,INC VXVISTA OPEN SOURCE;**14,24,60**;15 Mar 2016;Build 5
 ;Copyright 1995-2016,Document Storage Systems Inc. All Rights Reserved
 ;DSSBUILD - VFDHHL HL7 EXCHANGE 2011.1 * 04/26/11 * vxdev64v2k8_DEVXOS * T8
 ;
 ;LM - Re-namespace to VFDPXIM
 Q
VIMRPC(VFDRSLT,VFDVIMDA) ;[Remote procedure] - VFD IMMUNIZATION DEMOG
 ; Immplementing code copied from routine ^VFDHHLOT
 S VFDRSLT=""
 ;return demographics of a V IMMUNIZATION entry
 D CONT(.VFDRSLT,VFDVIMDA)
 Q
 ;
CONT(VFDRSLT,VFDVIMDA) ; Continuation of VIMRPC
 ; Copied from routine ^VFDZARAVVXU2
 N VFDRPC,TMP
 K VFDRPC S VFDRPC(0)=""
 D VIMREV(VFDVIMDA)
 S TMP=$NA(^XTMP("VFDVXA",$J)) K @TMP
 M @TMP=VFDRPC
 K VFDRSLT S VFDRSLT=TMP
 Q
 ;
VIMREV(VFDVIMDA) ; IMMUNIZATIONS
 ; Copied from routine ^VFDZARAVVXU2
 N VFDFILE,VFDSTR,VFDVALS,VFDMSG,XX,LEN,LINE,VFDFLDS,VFDMSG,VFDVFDDA,VFDTAR,X,XXX,Y
 S X="",XX=":                     IMMUNIZATIONS                        :"
 K LINE S $P(LINE,"=",$L(XX)+1)=""
 F Y=X,X,LINE,XX,LINE,X D STUFFRPC(Y)
 K DIC,DA,DR S DIC="^AUPNVIMM(",DA=VFDVIMDA
 S VFDFLDS=".01;.02;.03"
 S VFDFILE=9000010.11
 D GETS^DIQ(VFDFILE,VFDVIMDA,VFDFLDS,,"VFDTAR","VFDMSG")
 M VFDVALS=VFDTAR(VFDFILE,VFDVIMDA_",")
 D STUFFLDS(VFDFILE,VFDFLDS,.VFDVALS)
 I $$GET1^DIQ(VFDFILE,VFDVIMDA,1207,"I")'="" D
 . K VFDTAR
 . N LOT,LOTI,EXP,MANF
 . S LOT=$$GET1^DIQ(9000010.11,VFDVIMDA,.05,"E"),LOTI=$$GET1^DIQ(9000010.11,VFDVIMDA,.05,"I")
 . Q:LOT=""
 . S EXP=$$GET1^DIQ(9999999.41,LOTI,.09,"E")
 . S VFDSTR="LOT #;EXP DATE:"
 . S X=LOT_";"_EXP,VFDSTR=$$SETSTR^VALM1(X,VFDSTR,35,$L(X))
 . D STUFFRPC(VFDSTR)
 . S VFDSTR="MANUFACTURER:"
 . S X=$$GET1^DIQ(9999999.41,LOTI,.02,"E"),VFDSTR=$$SETSTR^VALM1(X,VFDSTR,35,$L(X))
 . D STUFFRPC(VFDSTR)
 ;
 K VFDTAR
 S VFDFLDS="1302;1303;1312"
 S VFDFILE=9000010.11
 D GETS^DIQ(VFDFILE,VFDVIMDA,VFDFLDS,,"VFDTAR","VFDMSG")
 I VFDTAR(VFDFILE,VFDVIMDA_",",1302)="",$D(^AUPNVIMM(VFDVIMDA,13)) S VFDTAR(VFDFILE,VFDVIMDA_",",1302)=$P(^AUPNVIMM(VFDVIMDA,13),U,2)
 I VFDTAR(VFDFILE,VFDVIMDA_",",1303)="",$D(^AUPNVIMM(VFDVIMDA,13)) S VFDTAR(VFDFILE,VFDVIMDA_",",1303)=$P(^AUPNVIMM(VFDVIMDA,13),U,3)
 K VFDVALS
 M VFDVALS=VFDTAR(VFDFILE,VFDVIMDA_",")
 D STUFFLDS(VFDFILE,VFDFLDS,.VFDVALS)
 S VFDSTR="ADMINISTERED BY:"
 S X=$$GET1^DIQ(VFDFILE,VFDVIMDA,1206,"E"),VFDSTR=$$SETSTR^VALM1(X,VFDSTR,35,$L(X))
 D STUFFRPC(VFDSTR)
 S VFDSTR="REACTION:"
 S X=$$GET1^DIQ(VFDFILE,VFDVIMDA,.06,"E"),VFDSTR=$$SETSTR^VALM1(X,VFDSTR,35,$L(X))
 D STUFFRPC(VFDSTR)
 S VFDSTR="ADMIN NOTES:"
 S VLP=$$GET1^DIQ(VFDFILE,VFDVIMDA,81101,"E"),LEN=$L(VLP)
 I LEN<50 S VFDSTR=$$SETSTR^VALM1(VLP,VFDSTR,35,$L(VLP))
 I LEN>=50 D SPLIT(VLP)
 ;
 ;VIS records
 ;
 N VIN,VISE
 S VIN=0 F  S VIN=$O(^AUPNVIMM(VFDVIMDA,2,"B",VIN)) Q:'VIN  D
 . S VISE=0 F  S VISE=$O(^AUPNVIMM(VFDVIMDA,2,"B",VIN,VISE)) Q:'VISE  D
 . . N LANG,VDATE,VREC,VIS,VISIEN,VISN,VISDT,VFDTARV,VFDVALS,VFDVIS,VFDVISFL,VNAME,VFDSTR
 . . S VISIEN=VISE_","_VFDVIMDA_",",VFDVISFL=9000010.112
 . . S VNAME=$$GET1^DIQ(920,VIN,.01,"E")
 . . S VREC=$$GET1^DIQ(VFDVISFL,VISIEN,.01,"I")
 . . S LANG="",LANG=$$GET1^DIQ(920,VREC,.04,"I")
 . . S:LANG'="" LANG=$$GET1^DIQ(.85,LANG,1,"E")
 . . S VDATE=$$GET1^DIQ(920,VREC,.02,"E")
 . . S X=VNAME_"  "_VDATE_"  "_LANG,VFDSTR="VIS OFFERED/GIVEN TO PATIENT:"
 . . S VFDSTR=$$SETSTR^VALM1(X,VFDSTR,35,$L(X))
 . . D STUFFRPC(VFDSTR)
 . . S VFDSTR="DATE OFFERED/GIVEN TO PATIENT:"
 . . S X=$$GET1^DIQ(VFDVISFL,VISIEN,.02,"E"),VFDSTR=$$SETSTR^VALM1(X,VFDSTR,35,$L(X))
 . . D STUFFRPC(VFDSTR)
 Q
 ;
STUFFRPC(VFDSTR) ;"VFDRPC" target needs to be preset
 N VFDLN
 S VFDLN=$O(VFDRPC(""),-1)+1
 S VFDRPC(VFDLN)="    "_VFDSTR
 Q
 ;
STUFFLDS(VFDFILE,VFDFLDS,VFDVALS) ; STUFFRPC
 N VFDFLD,I,VFDSTR
 F I=1:1 S VFDFLD=$P(VFDFLDS,";",I) Q:VFDFLD'>0  D SETSTR
 Q
 ;
SETSTR ;caled by STUFFLDS(VFDFILE,VFDFLDS,VFDTAR)
 ;given file, field(s)(.01)=, load field label(s) and value int VFDRPC
 N VFDFLDD,VFDSTR,X
 D FIELD^DID(VFDFILE,VFDFLD,"","LABEL","VFDFLDD","VFDMSG")
 S VFDSTR=VFDFLDD("LABEL")_":"
 S X=VFDVALS(VFDFLD)
 S VFDSTR=$$SETSTR^VALM1(X,VFDSTR,35,$L(X))
 D STUFFRPC(VFDSTR)
 Q
 ;
SPLIT(TXT) ; Wrap Admin Notes/Comments and break at spaces
 N CNT,J,STR,VLP,X1,X2
 S CNT=$L(TXT)/50
 S:CNT["." CNT=$P(CNT,".")+1,STR=1
 F J=1:1:CNT D
 . F X1=50:-1:0 Q:$E(TXT,X1)=" "
 . S:$L(TXT)>50 X2=$E(TXT,1,X1),TXT=$E(TXT,X1+1,245)
 . S:$L(TXT)<50 X2=TXT
 . S VFDSTR=$$SETSTR^VALM1(X2,VFDSTR,35,$L(X2))
 . D STUFFRPC(VFDSTR)
 . S VFDSTR=" "
 Q
 ;
 ;RAC - 10/4/15 modified to accomodate new and fields no longer in use
 ;RAC - 2/29/16 Redesign layout and add fields" Lot # expiration date,
 ;              Administred by, VIS offere/Given to patient and date
 ;              given to patient
 ;RAC -  3/4/16 Change Admin Notes from field 1 to field 81101 - Comments
 ;              To match the DLL.
 ;RAC - 3/15/16 Change Admin Notes to wrap in a block format verses one 
 ;              long string having a length of 50 characters.
 ;RAC - 3/15/16 Change Admin Notes to wrap in a block format verses and
 ;              break at space not just 50 characters.
